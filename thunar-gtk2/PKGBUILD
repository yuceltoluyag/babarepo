# Maintainer: Newb I the Newbd <czbd hostedon o2 halfacolon pl>
# Contributor: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Andrew Simmons <andrew.simmons@gmail.com>

_pkgbase=thunar
pkgname=(${_pkgbase}-gtk2)
pkgver=1.8.17
pkgrel=1
pkgdesc="Modern file manager for Xfce (GTK2 version)"
arch=('x86_64')
url="http://thunar.xfce.org"
license=('GPL2' 'LGPL2.1')
groups=('xfce4')
conflicts=("${_pkgbase}")
provides=("${_pkgbase}")
depends=('desktop-file-utils' 'libexif' 'hicolor-icon-theme' 'libnotify'
         'libgudev' 'gtk2' 'exo-git' 'libxfce4util' 'libxfce4ui' 'libpng')
makedepends=('intltool' 'xfce4-panel')
optdepends=('gvfs: for trash support, mounting with udisk and remote filesystems'
            'xfce4-panel: for trash applet'
            'tumbler: for thumbnail previews'
            'thunar-volman: manages removable devices'
            'thunar-archive-plugin-gtk2: create and deflate archives'
            'thunar-media-tags-plugin: view/edit id3/ogg tags')
source=(https://archive.xfce.org/src/xfce/thunar/${pkgver%.*}/thunar-$pkgver.tar.bz2)

sha256sums=('bf2afee581a8aa3489cbcd6c7ccf1db65e5d3f8c1d6ba0307278889940417dc2')

build() {
  cd "$srcdir/thunar-$pkgver"
  #patch -Np1 -i "${srcdir}/../thunar-1.6.3-pdl.patch"
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-gio-unix \
    --enable-dbus \
    --enable-gudev \
    --enable-notifications \
    --enable-exif \
    --enable-pcre \
    --disable-debug
  make
}

package() {
  cd "$srcdir/thunar-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
